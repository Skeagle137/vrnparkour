import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
}

group = 'net.skeagle'
version = '1.2.0'
description = 'VRNparkour'
sourceCompatibility = 16
targetCompatibility = 16

String mc = '1.17.1'

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = uri('https://jitpack.io') }
    mavenLocal()
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.36.0.2'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'net.skeagle:VRNLib:1.3'
    compileOnly "org.spigotmc:spigot-api:${mc}-R0.1-SNAPSHOT"
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}

task updatePlugins {
    doLast {
        copy {
            from 'build/libs'
            into directory
        }
    }
}

task deploy(dependsOn: ["jar", "updatePlugins"]) {
    updatePlugins.mustRunAfter jar
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
